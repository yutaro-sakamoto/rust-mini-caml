use crate::ast::{Exp, FunDef};
use crate::id::{Id, gen_tmp};
use crate::typedef::{Type, add_type, gen_type};
use std::vec;

grammar;

pub Exp: Box<Exp> = {
    "let" <var:IDENT> "=" <e1:Exp> "in" <e2:Exp>
        => Box::new(Exp::Let((var, gen_type()), e1, e2)),
    "let" "rec" <FunDef> "in" <Exp>
        => Box::new(Exp::LetRec(<>)),
    "let" "(" <PATTERN> ")" "=" <Exp> "in" <Exp>
        => Box::new(Exp::LetTuple(<>)),
    SemicolonExp
}

FunDef: FunDef = {
    <func_name:IDENT> <formal_args:FormalArgs> "=" <v:Exp> =>
    FunDef {
        name: add_type(func_name),
        args: formal_args,
        body: v,
    }
}

PATTERN: Vec<(Id, Type)> = {
    <mut v:PATTERN> "," <e:IDENT> => {v.push(add_type(e)); v},
    <e1:IDENT> "," <e2:IDENT> => vec![add_type(e1), add_type(e2)],
}

FormalArgs: Vec<(Id, Type)> = {
    IDENT => vec![add_type(<>)],
    <mut v:FormalArgs> <e:IDENT> => {v.push(add_type(e)); v}
}

SemicolonExp: Box<Exp> = {
    <l:SemicolonExp> ";" <r:IfExp>
        => Box::new(Exp::Let((gen_tmp(Type::Unit), Type::Unit), l, r)),
    IfExp
}

IfExp: Box<Exp> = {
    "if" <TupleExp> "then" <TupleExp> "else" <TupleExp> => Box::new(Exp::If(<>)),
    TupleExp
}

TupleExp: Box<Exp> = {
    <Elems> => Box::new(Exp::Tuple(<>)),
    CompExp
}

Elems: Vec<Box<Exp>> = {
    <CompExp> "," <CompExp> => vec![<>],
    <mut v:Elems> "," <e:CompExp> => {v.push(e); v},
}

Tier<Op,NextTier>: Box<Expr> = {
    Tier<Op,NextTier> Op NextTier => Box::new(Exp::Op(<>)),
    NextTier
};

CompExp: Box<Exp> = {
    <CompExp> "=" <AddSubExp> => Box::new(Exp::Eq(<>)),
    <CompExp> "<=" <AddSubExp> => Box::new(Exp::LE(<>)),
    <l:CompExp> ">=" <r:AddSubExp> => Box::new(Exp::LE(r, l)),
    <l:CompExp> "<" <r:AddSubExp> => Box::new(Exp::Not(Box::new(Exp::LE(r, l)))),
    <l:CompExp> ">" <r:AddSubExp> => Box::new(Exp::Not(Box::new(Exp::LE(l, r)))),
    <l:CompExp> "<>" <r:AddSubExp> => Box::new(Exp::Not(Box::new(Exp::Eq(l, r)))),
    AddSubExp
}

AddSubExp: Box<Exp> = {
    <AddSubExp> "+" <MulDivExp> => Box::new(Exp::Add(<>)),
    <AddSubExp> "-" <MulDivExp> => Box::new(Exp::Sub(<>)),
    <AddSubExp> "+." <MulDivExp> => Box::new(Exp::FAdd(<>)),
    <AddSubExp> "-." <MulDivExp> => Box::new(Exp::FSub(<>)),
    MulDivExp
}

MulDivExp: Box<Exp> = {
    <UnaryExp> "*." <UnaryExp> => Box::new(Exp::FMul(<>)),
    <UnaryExp> "/." <UnaryExp> => Box::new(Exp::FDiv(<>)),
    UnaryExp
}

UnaryExp: Box<Exp> = {
    "-" <v:AppExp> => match &(*v) {
        Exp::Float(f) => Box::new(Exp::Float(-f)),
        e => Box::new(Exp::Neg(v))
    },
    "-." <AppExp> => Box::new(Exp::FNeg(<>)),
    AppExp
}

AppExp: Box<Exp> = {
    SimpleExp ActualArgs => Box::new(Exp::App(<>)),
    <SimpleExp> "." "(" <Exp> ")" "<-" "(" <Exp> ")" => Box::new(Exp::Put(<>)),
    "Array.create" <SimpleExp> <SimpleExp> => Box::new(Exp::Array(<>)),
    "Not" <SimpleExp> => Box::new(Exp::Not(<>)),
    SimpleExp
}

ActualArgs: Vec<Box<Exp>> = {
    SimpleExp => vec![<>],
    <mut v:ActualArgs> <e:SimpleExp> => {v.push(e); v}
}

IDENT: String = {
    r"[a-zA-Z_][0-9a-zA-Z_]*" => <>.to_string()
}

SimpleExp: Box<Exp> = {
    "(" <Exp> ")" => <>,
    "(" ")" => Box::new(Exp::Unit),
    "true" => Box::new(Exp::Bool(true)),
    "false" => Box::new(Exp::Bool(false)),
    r"[0-9]+" => Box::new(Exp::Int(<>.parse().unwrap())),
    r"[0-9]+\.[0-9]*" => Box::new(Exp::Float(<>.parse().unwrap())),
    IDENT => Box::new(Exp::Var(<>)),
    <SimpleExp> "." "(" <Exp> ")" => Box::new(Exp::Get(<>)),
}